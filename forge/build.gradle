evaluationDependsOn(':common')

base {
    archivesName = "flywheel-${project.name}-${artifact_minecraft_version}"
}

loom {
    forge {
        mixinConfig "flywheel.backend.mixins.json"
        mixinConfig "flywheel.impl.mixins.json"
        mixinConfig "flywheel.impl.sodium.mixins.json"
    }

    runs {
        configureEach {
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
        }

        client {
            ideConfigGenerated true
            property 'flw.dumpShaderSource', 'true'
            property 'flw.debugMemorySafety', 'true'
            programArgs '--width', '1280', '--height', '720'
        }

        server {
            ideConfigGenerated true
            programArgs '--nogui'
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly project(path: ':common', configuration: 'namedElements')

    modCompileOnly "maven.modrinth:embeddium:$embeddium_version"
    modCompileOnly "maven.modrinth:oculus:$oculus_version"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

tasks.withType(JavaCompile).configureEach {
    source(project(':common').sourceSets.main.allSource)
}

sourcesJar.configure {
    from(project(':common').sourceSets.main.allSource)
}

javadoc.configure {
    source(project(':common').sourceSets.main.allJava)
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from(components["java"])
            artifactId = "flywheel-${project.name}-intermediary-${artifact_minecraft_version}"
        }
    }
    repositories {
        maven {
            url "file://${rootProject.projectDir}/mcmodsrepo"
        }

        if (project.hasProperty('mavendir')) {
            maven { url rootProject.file(property('mavendir')) }
        }
    }
}
